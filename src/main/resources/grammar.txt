program              →  declaration* EOF
declaration          →  variableDeclaration | functionDeclaration | statement
variableDeclaration  →  "var" IDENTIFIER ( "=" expression )? ";"
functionDeclaration  →  "fun" function
function             →  IDENTIFIER "(" parameters? ")" block
parameters           →  IDENTIFIER ( "," IDENTIFIER )*
statement            →  expressionStatement
                     |  block
                     |  ifStatement
                     |  whileStatement
                     |  forStatement
                     |  printStatement
                     |  breakStatement
                     |  continueStatement

block                →  "{" declaration* "}"
ifStatement          →  "if" "(" expression ")" statement ( "else" statement )?
whileStatement       →  "while" "(" expression ")" statement
forStatement         →  "for" "(" (variableDeclaration | expressionStatement | ";") expression? ";" expression? ";" ")" statement
printStatement       →  "print" expression ";"
breakStatement       →  "break" ";"
continueStatement    →  "continue" ";"
expressionStatement  →  expression ";"
expression           →  assignment
assignment           →  IDENTIFIER "=" assignment | logicOr
logicOr              →  logicAnd ( "or" logicAnd )* ;
logicAnd             →  equality ( "and" equality )* ;
equality             →  comparison ( ( "!=" | "==" ) comparison )*
comparison           →  term ( ( ">" | ">=" | "<" | "<=" ) term )*
term                 →  factor ( ( "-" | "+" ) factor )*
factor               →  unary ( ( "/" | "*" ) unary )*
unary                →  ( "!" | "-" ) unary | call
call                 →  primary ( "(" arguments? ")" )*
arguments            →  expression ( "," expression )*
primary              →  IDENTIFIER | NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")"